static point p1start = Point(15, 45);
static point p2start = Point(85, 45);

static region p1BuildRegion = RegionRect(PointGetX(p1start) - 15, PointGetY(p1start) - 25,
                                         PointGetX(p1start) + 15, PointGetY(p1start) + 25);
static region p2BuildRegion = RegionRect(PointGetX(p2start) - 15, PointGetY(p2start) - 25,
                                         PointGetX(p2start) + 15, PointGetY(p2start) + 25);

void issueTrain(unit building) {
  if (UnitGetType(building) == "Barracks") {
    gg_issueOrder(building, "barracks train marine", "replace");
  } else if (UnitGetType(building) == "EngineeringBay") {
    gg_issueOrder(building, "barracks train reaper", "replace");
  } else if (UnitGetType(building) == "Bunker") {
    gg_issueOrder(building, "barracks train marauder", "replace");
  } else {
    gg_log("Unhandled building type: ");
    gg_log(UnitGetType(building));
  }
}

bool trigger_trained(bool testConds, bool runActions) {
  point target;
  if (!runActions) { return true; }
  if (UnitGetOwner(EventUnitProgressUnit()) == 1) {
    target = p2start;
  } else {
    target = p1start;
  }
  issueTrain(EventUnit());
  gg_issueOrderToPoint(EventUnitProgressUnit(), "attack", target, "replace");
  return true;
}

bool trigger_constructed(bool testConds, bool runActions) {
  if (!runActions) { return true; }
  issueTrain(EventUnitProgressUnit());
  return true;
}

bool trigger_cc_died(bool testConds, bool runActions) {
  if (!runActions) { return true; }
  if (UnitGetType(EventUnit()) != "CommandCenter") { return true; }
  if (UnitGetOwner(EventUnit()) == 1) {
    gg_gameOver_victory(2);
    gg_gameOver_defeat(1);
  } else {
    gg_gameOver_victory(1);
    gg_gameOver_defeat(2);
  }
  return true;
}

bool trigger_unitDied(bool testConds, bool runActions) {
  int player;
  string unitType;
  if (!runActions) { return true; }

  player = UnitGetOwner(EventUnitDamageSourceUnit());
  unitType = UnitGetType(EventUnit());

  if (unitType == "Barracks") {
    gg_incPlayerMinerals(player, 50);
  } else if (unitType == "EngineeringBay") {
    gg_incPlayerMinerals(player, 50);
  } else if (unitType == "Bunker") {
    gg_incPlayerMinerals(player, 50);
  } else if (unitType == "Marine") {
    gg_incPlayerMinerals(player, 1);
  } else if (unitType == "Marauder") {
    gg_incPlayerMinerals(player, 2);
  } else if (unitType == "Reaper") {
    gg_incPlayerMinerals(player, 1);
  } else if (unitType == "CommandCenter") {
    // .
  } else {
    gg_log("Missing bounty for unit: ");
    gg_log(unitType);
  }
  return true;
}

bool trigger_leftRegion(bool testConds, bool runActions) {
  if (!runActions) { return true; }
  if (UnitGetType(EventUnit()) != "SCV") { return true; }
  UnitSetPosition(EventUnit(), RegionGetCenter(EventUnitRegion()), true);
  return true;
}

void initPlayer(int player, point start, region buildRegion) {
  unit cc;
  unit scv;
  cc = gg_createUnitAtPoint(player, "CommandCenter", start);
  scv = gg_createUnitAtPoint(player, "SCV", start);
  gg_setPlayerMinerals(player, 500);
  gg_event_unitLeftRegion("trigger_leftRegion", UnitRefFromUnit(scv), buildRegion);
}

void gg_MapInit() {
  initPlayer(1, p1start, p1BuildRegion);
  initPlayer(2, p2start, p2BuildRegion);

  gg_event_unitTrained("trigger_trained", null);
  gg_event_unitConstructed("trigger_constructed", null);
  gg_event_unitDied("trigger_cc_died", null);
  gg_event_unitDied("trigger_unitDied", null);
}
